create project Voiture

create application Voiture


create module Paramétrage


// *********************
// Entité Modèle voiture
// *********************

create entity Modèle voiture
set icon car
add field Nom
set field Nom required
add field Description with type text


// *********************
// Entité Langue
// *********************

create entity Langue
set icon flag
add field Nom
set field Nom required


// *********************
// Entité Entreprise
// *********************

create entity Entreprise
set icon building-o
add field Nom
set field Nom required


create module Extranet


select module home



// *********************
// Entité Agenda
// *********************

add component Agenda


// *********************
// Entité Voiture
// *********************

create entity Voiture
set icon cab
add field Modèle related to Modèle voiture using Nom
add field Immatriculation
add field Hors service with type boolean
add field Date achat with type date
add field Kilométrage relevé with type number
add field Date dernier relevé with type date
add field Nombre de places with type number
add field Nombre de bagages with type number
add field Wifi with type boolean
add field Téléphone with type boolean
add field Siège enfant with type boolean

add component localfilestorage with name Documents



// *********************
// Entité Chauffeur
// *********************

create entity Chauffeur
set icon user
add field Civilité with type enum and values Monsieur, Madame and default value Monsieur
add field Nom
set field Nom required
add field Prénom
add field Rue
add field Complément rue
add field Lieu-dit
add field Code postal
add field Ville

add field Langue related to many Langue using Nom

add field Voiture related to Voiture using Immatriculation

add component localfilestorage with name Documents


// *********************
// Entité Demande de réservation
// *********************

create entity Demande
set icon comment-o
add field Entreprise related to Entreprise using Nom
add field Jour with type date and default value today
add field Heure with type time
add field Nom du client
set field Nom du client required
add field Lieu
add field Exigence particulière with type text


add widget stats on entity Chauffeur
add widget stats on entity Voiture
add widget stats on entity Demande

add widget last records on entity Demande limited to 10 records with columns Entreprise, Demandeur, Jour, Heure, Nom du client, Lieu



select entity Demande
add field Demandeur related to User using login

select entity User
add field Chauffeur related to Chauffeur using Nom, Prénom

